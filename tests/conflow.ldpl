# conflow.ldpl
# test basic control flow statements
# part of the LDPL Programming Language Test Battery
# by Martín del Río
# https://github.com/lartu/ldpltest

DATA:
numVar1 is NUMBER
txtVar2 is TEXT
myVar/3 is NUMBER
myVar/4 is TEXT
txtList is TEXT LIST
numList is NUMBER LIST
numMap is NUMBER MAP

PROCEDURE:
# Test STORE
sub-procedure test1
    store 416 in numVar1
    display numVar1 crlf
    store 518.121 in myVar/3
    display myVar/3 crlf
    store myVar/3 in numVar1
    display numVar1 " " myVar/3 crlf
    store "Hi\tthere!" in txtVar2
    display txtVar2 crlf
    store txtVar2 in numVar1
    display numVar1 crlf
    store "-12.6" in myVar/4
    store myVar/4 in myVar/3
    display myVar/4 " " myVar/3 crlf
    store "12.6" in myVar/4
    store myVar/4 in myVar/3
    display myVar/4 " " myVar/3 crlf
    store "12" in myVar/4
    store myVar/4 in myVar/3
    display myVar/4 " " myVar/3 crlf
    store "0" in myVar/4
    display myVar/4 crlf
    store crlf in txtVar2
    display "---" txtVar2 "." crlf
end sub-procedure

sub-procedure test2
    store 10 in numVar1

    if 1 is less than numVar1 then
        display "OK" crlf
    end if
    if 100 is less than numVar1 then
        display "NO" crlf
    end if

    if 100 is greater than numVar1 then
        display "OK" crlf
    end if
    if 1 is greater than numVar1 then
        display "NO" crlf
    end if

    if 10 is less than or equal to numVar1 then
        display "OK" crlf
    end if
    if 10 is less than numVar1 then
        display "NO" crlf
    end if

    if 10 is greater than or equal to numVar1 then
        display "OK" crlf
    end if
    if 10 is greater than numVar1 then
        display "NO" crlf
    end if

    if 10 is equal to numVar1 then
        display "OK" crlf
    end if
    if 10 is not equal to numVar1 then
        display "NO" crlf
    end if

    store "hi there! こんにちは!" in txtVar2
    if txtVar2 is equal to "hi there! こんにちは!" then
        display "OK" crlf
    end if
    if txtVar2 is equal to "hi there!" then
        display "NO" crlf
    end if
    if txtVar2 is not equal to "hi there!" then
        display "OK" crlf
    end if
    if txtVar2 is not equal to "hi there! こんにちは!" then
        display "OK" crlf
    end if

    store 10 in numVar1

    store 1 in myVar/3
    if myVar/3 is less than numVar1 then
        display "OK" crlf
    end if

    store 100 in myVar/3
    if myVar/3 is less than numVar1 then
        display "NO" crlf
    end if

    store 100 in myVar/3
    if myVar/3 is greater than numVar1 then
        display "OK" crlf
    end if
    store 1 in myVar/3
    if myVar/3 is greater than numVar1 then
        display "NO" crlf
    end if

    store 10 in myVar/3
    if myVar/3 is less than or equal to numVar1 then
        display "OK" crlf
    end if
    store 10 in myVar/3
    if myVar/3 is less than numVar1 then
        display "NO" crlf
    end if

    store 10 in myVar/3
    if myVar/3 is greater than or equal to numVar1 then
        display "OK" crlf
    end if
    store 10 in myVar/3
    if myVar/3 is greater than numVar1 then
        display "NO" crlf
    end if

    store 10 in myVar/3
    if myVar/3 is equal to numVar1 then
        display "OK" crlf
    end if
    store 44 in myVar/3
    if myVar/3 is not equal to numVar1 then
        display "OK" crlf
    end if

    store "hi there! こんにちは!" in txtVar2
    store "hi there! こんにちは!" in myVar/4
    if txtVar2 is equal to myVar/4 then
        display "OK" crlf
    end if
    store "hi there!" in myVar/4
    if txtVar2 is equal to myVar/4 then
        display "NO" crlf
    end if
    store "hi there!" in myVar/4
    if txtVar2 is not equal to myVar/4 then
        display "OK" crlf
    end if
    store "hi there! こんにちは!" in myVar/4
    if txtVar2 is not equal to myVar/4 then
        display "OK" crlf
    end if

    store 0 in numVar1
    while numVar1 is less than 100.5 do
        display "LOOP A" crlf
        add 0.78 and numVar1 in numVar1
    repeat

    store 0 in numVar1
    while numVar1 is less than or equal to 100 do
        display "LOOP B" crlf
        add 1 and numVar1 in numVar1
    repeat

    store 0 in numVar1
    while numVar1 is less than or equal to 100 do
        display "LOOP B" crlf
        add 1 and numVar1 in numVar1
        continue
        store 0 in numVar1
    repeat

    store 0 in numVar1
    while numVar1 is greater than 100 do
        display "LOOP C" crlf
        add 1 and numVar1 in numVar1
    repeat

    store "" in myVar/4
    while myVar/4 is not equal to "AAAAAAAAAAAAAA" do
        join myVar/4 and "A" in myVar/4
        display myVar/4 crlf
    repeat

    while 1 is equal to 1 do
        display "OK" crlf
        break
        display "NO" crlf
    repeat

    store 0 in numVar1
    if numVar1 is equal to 1 then
        display "NO" crlf
    else if numVar1 is equal to 0 then
        display "OK" crlf
    end if

    store 123 in numVar1
    if numVar1 is equal to 1 then
        display "NO" crlf
    else if numVar1 is equal to 0 then
        display "NO" crlf
    else if numVar1 is equal to 4 then
        display "NO" crlf
    else if numVar1 is equal to 5 then
        display "NO" crlf
    end if

    store 123 in numVar1
    if numVar1 is equal to 1 then
        display "NO" crlf
    else if numVar1 is equal to 0 then
        display "NO" crlf
    else if numVar1 is equal to 4 then
        display "NO" crlf
    else if numVar1 is equal to 123 then
        display "OK" crlf
    else
        display "NO" crlf
    end if

    for numVar1 from 0 to 10 step 2 do
        if numVar1 is greater than 7 then
            break
        end if
        display "FOR A " numVar1 crlf
    repeat

    for numVar1 from 5 to 0 step -1 do
        if numVar1 is equal to 3 then
            continue
        end if
        display "FOR B " numVar1 crlf
    repeat

    for numVar1 from 3 to 3 step 1 do
        display "FOR C " numVar1 crlf
    repeat

    for numVar1 from 0 to -1 step 1 do
        display "FOR D " numVar1 crlf
    repeat

    for numVar1 from 1 to 200 step numVar1 do
        display "FOR E " numVar1 crlf
    repeat

    push "A" to txtList
    push "B" to txtList
    push "C" to txtList
    for each txtVar2 in txtList do
        display "FOR EACH A " txtVar2 crlf
    repeat

    push 1 to numList
    push 2 to numList
    push 3 to numList
    push 4 to numList
    push 5 to numList
    for each numVar1 in numList do
        display "FOR EACH B " numVar1 crlf
        if numVar1 is equal to 3 then
            break
        end if
    repeat

    store 1 in numMap:"-"
    store 3 in numMap:"+"
    store 5 in numMap:"*"
    store 7 in numMap:"/"
    store 0 in myVar/3
    for each numVar1 in numMap do
        if numVar1 is equal to 5 then
            continue
        end if
        add numVar1 and myVar/3 in myVar/3
    repeat
    display "FOR EACH C " myVar/3 crlf

    return
    exit
end sub-procedure

goto realTests
call test2
call sub-procedure test1
display "NOOOOOOOOOOO!" crlf
label end
display "Okay!" crlf
exit
label realTests
call sub-procedure test1
call test2
goto end
